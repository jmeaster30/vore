--(

  Email Addresses (Basic and Official RFC 5322)

  Basic Regex: \b[A-Z0-9._%+-]+@[A-Z0-9.-]+\.[A-Z]{2,}\b
This is from https://www.regular-expressions.info/email.html which I have used extensively to learn about regular expressions.

The author of this article does mention there have been many bug reports about this but we will also
try out the official RFC 5322 email regular expression next.

)--

find all
  word start
  at least 1 in letter, digit, ".", "_", "%", "+", "-"
  "@"
  at least 1 in letter, digit, ".", "-"
  "."
  at least 2 letter
  word end

--(

  RFC5322 Regex: 
\A(?:[a-z0-9!#$%&'*+/=?^_‘{|}~-]+(?:\.[a-z0-9!#$%&'*+/=?^_‘{|}~-]+)*
 |  "(?:[\x01-\x08\x0b\x0c\x0e-\x1f\x21\x23-\x5b\x5d-\x7f]
      |  \\[\x01-\x09\x0b\x0c\x0e-\x7f])*")
@ (?:(?:[a-z0-9](?:[a-z0-9-]*[a-z0-9])?\.)+[a-z0-9](?:[a-z0-9-]*[a-z0-9])?
  |  \[(?:(?:25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)\.){3}
       (?:25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?|[a-z0-9-]*[a-z0-9]:
          (?:[\x01-\x08\x0b\x0c\x0e-\x1f\x21-\x5a\x53-\x7f]
          |  \\[\x01-\x09\x0b\x0c\x0e-\x7f])+)
     \])\z
Again this is from https://www.regular-expressions.info/email.html

I will make this match all instances instead of expecting the input itself to be a full exact match

I will also start by defining some helpers to make this more readable

)--

set localPart to pattern
  in letter, digit, "!", "#", "$", "%", 
    "&", "'", "*", "+", "/", "=", "?", 
    "^", "_", "‘", "{", "|", "}", "~", "-"  -- it is a little long to write but "verbose" is in the name

set hexPart1 to pattern
  in "\x01" to "\x08", "\x0b", "\x0C", 
    "\x0e" to "\x1f", "\x21",
    "\x23" to "\x5b", "\x5d" to "\x7f"

set hexPart2 to pattern
  in "\x01" to "\x09", "\x0b", "\x0C", 
    "\x0e" to "\x7f"

set ld to pattern
  in letter, digit

set ldd to pattern
  in letter, digit, "-"

find all 
  (at least 1 localPart at least 0 ("." at least 1 localPart))
  or
  ('"' at least 0 (hexPart1 or ('\\' hexPart2)) '"')
  "@"
  ((at least 1 (ld (maybe ((at least 0 ldd) ld)) ".")) ld (maybe ((at least 0 ldd) ld))) 
  or
  ("["
    exactly 3 (
      ("25" in "0" to "5") or 
      ("2" in "0" to "4" digit) or 
      (maybe ("0" or "1") digit maybe digit)
      "."
    ) (
      ("25" in "0" to "5") or 
      ("2" in "0" to "4" digit) or 
      (maybe ("0" or "1") digit maybe digit) or 
      (maybe (at least 0 ldd ld) ":" at least 1 (hexPart1 or ("\\" hexPart2)))
    )
  "]")
